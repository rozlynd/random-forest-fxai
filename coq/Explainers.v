Require Import Bool.

From RFXP Require Import Utils Xp.

(* Base definitions *)

Module ExplainersDefs (Import E : InputProblem).
    Include ExplanationsDefs E.

End ExplainersDefs.

Module EnumeratorsDefs (Import E : InputProblem).
    Include ExplainersDefs E.

    Variant Xp : Type :=
    | isAXp (X : S.t) : Xp
    | isCXp (X : S.t) : Xp.

    Definition isXp (X : Xp) : Prop :=
        match X with
        | isAXp X => AXp X
        | isCXp X => CXp X
        end.

End EnumeratorsDefs.


(* Find minimal explanations from a seed (eliminate redundant features) *)

Module Type AXpFinderBaseOn (Import E : InputProblem).
    Module Import Xp := ExplainersDefs E.

    Parameter findAXp : S.t -> S.t.
End AXpFinderBaseOn.

Module Type AXpFinderOn (Import E : InputProblem) <: AXpFinderBaseOn E.
    Include AXpFinderBaseOn E.

    Axiom findAXpSound :
        forall X, Xp.WAXp X -> Xp.AXp (findAXp X).

    Axiom findAXpSane :
        forall X, S.Subset (findAXp X) X.

End AXpFinderOn.

Module Type AXpFinderBase.
    Declare Module E : InputProblem.
    Include AXpFinderBaseOn E.
End AXpFinderBase.

Module Type AXpFinder <: AXpFinderBase.
    Declare Module E : InputProblem.
    Include AXpFinderOn E.
End AXpFinder.


Module Type CXpFinderBaseOn (Import E : InputProblem).
    Module Import Xp := ExplainersDefs E.

    Parameter findCXp : S.t -> S.t.
End CXpFinderBaseOn.

Module Type CXpFinderOn (Import E : InputProblem) <: CXpFinderBaseOn E.
    Include CXpFinderBaseOn E.

    Axiom findCXpSound :
        forall X, Xp.WCXp X -> Xp.CXp (findCXp X).

    Axiom findCXpSane :
        forall X, S.Subset (findCXp X) X.

End CXpFinderOn.

Module Type CXpFinderBase.
    Declare Module E : InputProblem.
    Include CXpFinderBaseOn E.
End CXpFinderBase.

Module Type CXpFinder <: CXpFinderBase.
    Declare Module E : InputProblem.
    Include CXpFinderOn E.
End CXpFinder.


(* Decide if a set is a WCXp *)

Module Type WCXpCheckerBaseOn (Import E : InputProblem).
    Module Import Xp := ExplainersDefs E.

    Parameter checkWCXp : S.t -> bool.
End WCXpCheckerBaseOn.

Module Type WCXpCheckerOn (Import E : InputProblem) <: WCXpCheckerBaseOn E.
    Include WCXpCheckerBaseOn E.

    Axiom checkWCXpSound :
        forall X, Bool.reflect (Xp.WCXp X) (checkWCXp X).

End WCXpCheckerOn.

Module Type WCXpCheckerBase.
    Declare Module E : InputProblem.
    Include WCXpCheckerBaseOn E.
End WCXpCheckerBase.

Module Type WCXpChecker <: WCXpCheckerBase.
    Declare Module E : InputProblem.
    Include WCXpCheckerOn E.
End WCXpChecker.


(* Default implementations of finders given a WCXp checker *)

Module AXpIterativeFinderBaseOn (Import E : InputProblem) (Chk : WCXpChecker with Module E := E) <: AXpFinderBaseOn E.
    Module Import Xp := ExplainersDefs E.

    Local Definition checkWAXp X := negb (Chk.checkWCXp (S.compl X)).

    Definition findAXp := S.shrink checkWAXp.

End AXpIterativeFinderBaseOn.

Module AXpIterativeFinderOn (Import E : InputProblem) (Chk : WCXpChecker with Module E := E) : AXpFinderOn E.
    Module Impl := AXpIterativeFinderBaseOn E Chk.
    Include Impl.

    Lemma checkWAXp_reflect :
        forall X, Bool.reflect (Xp.WAXp X) (checkWAXp X).
    Admitted.

    Lemma findAXp_isWAXp :
        forall X, Xp.WAXp X -> Xp.WAXp (findAXp X).
    Proof.
        intros X H;
        apply (Bool.reflect_iff _ _ (checkWAXp_reflect X)),
            S.shrink_spec2 in H;
        now destruct (checkWAXp_reflect (S.shrink checkWAXp X)).
    Qed.

    Theorem findAXpSound :
        forall X, Xp.WAXp X -> Xp.AXp (findAXp X).
    Proof.
        intros X H; unfold findAXp; split.
        -   now apply findAXp_isWAXp.
        -   intros Y HSubs HY;
            apply S.shrink_spec3 with (p := checkWAXp);
                try (now apply (Bool.reflect_iff _ _ (checkWAXp_reflect _)));
                assumption.
    Qed.

    Theorem findAXpSane :
        forall X, E.S.Subset (findAXp X) X.
    Proof. apply S.shrink_spec1. Qed.

End AXpIterativeFinderOn.

Module AXpIterativeFinder (E_ : InputProblem) (Chk : WCXpChecker with Module E := E_) : AXpFinder with Module E := E_.
    Module E := E_.
    Module Impl := AXpIterativeFinderOn E_ Chk.
    Include Impl.
End AXpIterativeFinder.


Module CXpIterativeFinderBaseOn (Import E : InputProblem) (Chk : WCXpChecker with Module E := E) <: CXpFinderBaseOn E.
    Module Import Xp := ExplainersDefs E.

    Definition findCXp := S.shrink Chk.checkWCXp.

End CXpIterativeFinderBaseOn.

Module CXpIterativeFinderOn (Import E : InputProblem) (Chk : WCXpChecker with Module E := E) : CXpFinderOn E.
    Module Impl := CXpIterativeFinderBaseOn E Chk.
    Include Impl.

    Lemma findCXp_isWCXp :
        forall X, Xp.WCXp X -> Xp.WCXp (findCXp X).
    Proof.
        intros X H;
        apply (Bool.reflect_iff _ _ (Chk.checkWCXpSound X)),
            S.shrink_spec2 in H;
        now destruct (Chk.checkWCXpSound (S.shrink Chk.checkWCXp X)).
    Qed.

    Theorem findCXpSound :
        forall X, Xp.WCXp X -> Xp.CXp (findCXp X).
    Proof.
        intros X H; unfold findCXp; split.
        -   now apply findCXp_isWCXp.
        -   intros Y HSubs HY;
            apply S.shrink_spec3 with (p := Chk.checkWCXp);
                try (now apply (Bool.reflect_iff _ _ (Chk.checkWCXpSound _)));
                assumption.
    Qed.

    Theorem findCXpSane :
        forall X, E.S.Subset (findCXp X) X.
    Proof. apply S.shrink_spec1. Qed.

End CXpIterativeFinderOn.

Module CXpIterativeFinder (E_ : InputProblem) (Chk : WCXpChecker with Module E := E_) : CXpFinder with Module E := E_.
    Module E := E_.
    Module Impl := CXpIterativeFinderOn E_ Chk.
    Include Impl.
End CXpIterativeFinder.


(* Enumerate all explanations *)

Module Type EnumeratorBase (Import E : InputProblem).
    Module Import Xp := EnumeratorsDefs E.

    Parameter getNew : list Xp -> option Xp.
End EnumeratorBase.

Module Type Enumerator (Import E : InputProblem) <: EnumeratorBase E.
    Include EnumeratorBase E.

    Axiom getNewSound :
        forall Xs X, getNew Xs = Some X -> Xp.isXp X.

    Axiom getNewComplete :
        forall Xs X, getNew Xs = None -> Xp.isXp X -> List.In X Xs.
            
End Enumerator.


Module DummyExplainer (Import E : InputProblem) : EnumeratorBase E.
    Module Import Xp := EnumeratorsDefs E.

    Definition getNew (l : list Xp) := Some (isAXp E.S.all).
End DummyExplainer.